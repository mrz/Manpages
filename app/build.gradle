apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.ben-manes.versions'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "mrz.android.manpages"

        minSdkVersion 15
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

def supportLibraryVersion = '22.2.0'
def mosbyVersion = '1.1.0'
def daggerVersion = '2.0.1'

dependencies {
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Support libraries
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"

    // RX
    compile 'io.reactivex:rxkotlin:0.22.12'

    // MVP
    compile "com.hannesdorfmann.mosby:core:$mosbyVersion"
    compile "com.hannesdorfmann.mosby:mvp:$mosbyVersion"
    compile "com.hannesdorfmann.mosby:rx:$mosbyVersion"

    // DI
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Utils
    apt 'com.hannesdorfmann.fragmentargs:processor:2.1.2'
    compile 'com.firebase:firebase-client-android:2.3.0'
    compile 'com.google.auto.factory:auto-factory:1.0-beta2'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'io.realm:realm-android:0.80.3'
}
